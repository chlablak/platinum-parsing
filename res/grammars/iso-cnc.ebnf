(* ISO CNC grammar (EBNF) *)
(* https://en.wikipedia.org/wiki/G-code *)

(* Statements *)
statements list = statement with com, { "\n", statement with com }, [ "\n" ] ;
statement with com = statement | comment ;
statement = commands list, [ print ]
          | "(", statement, ")" ;

(* Commands *)
commands list = command, { " ", command } ;
print = "(", string, ")" ;
command = code
        | macro, parameter
        | macro, parameters list
        | signal ;
parameters list = "[", parameter, { " ", parameter }, "]" ;
parameter = uppercase string, [ number ] ;
macro = "#", uppercase string ;
signal = "%", [ string ] ;
code = uppercase, [ number ] ;

(* Comments *)
comment = ";", string ;

(* Strings and characters *)
string = character, { character } ;
uppercase string = uppercase, { uppercase } ;
character = letter | digit | " " | "." | "_" ;
letter = lowercase | uppercase ;
lowercase = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k"
          | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v"
          | "w" | "x" | "y" | "z" ;
uppercase = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K"
          | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V"
          | "W" | "X" | "Y" | "Z" ;

(* Numbers *)
number = [ sign ], ( natural | real ) ;
sign = "-" | "+" ;
real = [ natural ], ".", natural
     | natural, ".", [ natural ] ;
natural = digit, { digit } ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
